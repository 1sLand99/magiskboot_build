find_program(RUSTC_EXECUTABLE rustc REQUIRED)
execute_process(
    COMMAND ${RUSTC_EXECUTABLE} -Vv
    OUTPUT_VARIABLE "RUSTC_OUTPUT"
    ERROR_QUIET
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
string(REPLACE "\n" ";" RUSTC_OUTPUT ${RUSTC_OUTPUT})
foreach(RUSTC_OUTPUT_LINE ${RUSTC_OUTPUT})
    string(REGEX REPLACE "^[ ]+" "" RUSTC_OUTPUT_LINE ${RUSTC_OUTPUT_LINE})
    if (RUSTC_OUTPUT_LINE STREQUAL "")
        continue()
    else()
        if (RUSTC_OUTPUT_LINE MATCHES "^rustc[ \t]+")
            string(REGEX MATCH "[0-9]+\\.[0-9]+\\.[0-9]+" RUSTC_VERSION ${RUSTC_OUTPUT_LINE})
        elseif (RUSTC_OUTPUT_LINE MATCHES "^host:[ \t]+")
            string(REGEX REPLACE "^host:[ \t]+" "" HOST_RUSTC_TARGET ${RUSTC_OUTPUT_LINE})
            message(STATUS "Host Rust target: ${HOST_RUSTC_TARGET}")
            if (NOT RUSTC_TARGET)
                set(RUSTC_TARGET "${HOST_RUSTC_TARGET}")
            endif()
        endif()
    endif()
endforeach()
if (NOT DEFINED RUSTC_VERSION)
    set(RUSTC_VERSION "unknown")
endif()
message(STATUS "Found rustc: ${RUSTC_EXECUTABLE} (found version ${RUSTC_VERSION})")
if (RUSTC_TARGET)
    message(STATUS "Rust target: ${RUSTC_TARGET}")
else()
    message(FATAL_ERROR "Rust target cannot be detected")
endif()
find_program(CARGO_EXECUTABLE cargo REQUIRED)
execute_process(
    COMMAND ${CARGO_EXECUTABLE} -V
    OUTPUT_VARIABLE "CARGO_VERSION"
    ERROR_QUIET
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
if (CARGO_VERSION MATCHES "^cargo[ \t]+")
    string(REGEX REPLACE "^cargo[ \t]+" "" CARGO_VERSION ${CARGO_VERSION})
else()
    set(CARGO_VERSION "unknown")
endif()
message(STATUS "Found cargo: ${CARGO_EXECUTABLE} (found version ${CARGO_VERSION})")

if (CMAKE_BUILD_TYPE MATCHES "Debug")
    message(STATUS "Rust build will be unoptimized")
    set(CARGO_OUT "debug")
else()
    message(STATUS "Rust build will be optimized")
    set(CARGO_FLAGS ${CARGO_FLAGS} -r)
    set(CARGO_OUT "release")
endif()

if (CMAKE_BUILD_TYPE MATCHES "MinSize")
    message(STATUS "Rust build will be optimized for size")
    set(RUSTFLAGS "${RUSTFLAGS} -Copt-level=z")
endif()

if (CMAKE_BUILD_TYPE MATCHES "WithDebInfo")
    message(STATUS "Debug info enabled for Rust build")
    set(RUSTFLAGS "${RUSTFLAGS} -g")
endif()

set(RUSTFLAGS "${RUSTFLAGS} -A dead_code -A unused_imports -A redundant_semicolons")

if (RUST_BUILD_STD)
    message(STATUS "Building Rust STD from source")
    if (EMSCRIPTEN)
        set(CARGO_FLAGS ${CARGO_FLAGS} -Zbuild-std=std,panic_abort)
        set(RUSTFLAGS "${RUSTFLAGS} -Cpanic=abort")
    else()
        set(CARGO_FLAGS ${CARGO_FLAGS} -Zbuild-std)
    endif()
else()
    message(STATUS "Using prebuilt Rust STD")
endif()

if (USE_LTO_LINKER_PLUGIN)
    message(STATUS "Full LTO enabled")
    set(RUSTFLAGS "${RUSTFLAGS} -Clto=yes")
    set(RUSTFLAGS "${RUSTFLAGS} -Clinker-plugin-lto=yes")
    set(RUSTFLAGS "${RUSTFLAGS} -Cembed-bitcode=yes")
else()
    message(STATUS "Full LTO disabled")
    set(RUSTFLAGS "${RUSTFLAGS} -Clto=no")
    set(RUSTFLAGS "${RUSTFLAGS} -Clinker-plugin-lto=no")
    set(RUSTFLAGS "${RUSTFLAGS} -Cembed-bitcode=no")
endif()

message(STATUS "RUSTFLAGS: ${RUSTFLAGS}")
message(STATUS "CARGO_FLAGS: ${CARGO_FLAGS}")
