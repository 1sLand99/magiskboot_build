find_program(RUSTC_EXECUTABLE rustc REQUIRED)
execute_process(
    COMMAND ${RUSTC_EXECUTABLE} -Vv
    OUTPUT_VARIABLE "RUSTC_OUTPUT"
    ERROR_QUIET
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
string(REPLACE "\n" ";" RUSTC_OUTPUT ${RUSTC_OUTPUT})
foreach(RUSTC_OUTPUT_LINE ${RUSTC_OUTPUT})
    string(REGEX REPLACE "^[ ]+" "" RUSTC_OUTPUT_LINE ${RUSTC_OUTPUT_LINE})
    if (RUSTC_OUTPUT_LINE STREQUAL "")
        continue()
    else()
        if (RUSTC_OUTPUT_LINE MATCHES "^rustc[ \\t]+")
            string(REGEX MATCH "[0-9]+\\.[0-9]+\\.[0-9]+" RUSTC_VERSION ${RUSTC_OUTPUT_LINE})
        elseif (RUSTC_OUTPUT_LINE MATCHES "^host:[ \\t]+")
            string(REGEX REPLACE "^host:[ \\t]+" "" RUSTC_TARGET ${RUSTC_OUTPUT_LINE})
        endif()
    endif()
endforeach()
if (NOT DEFINED RUSTC_VERSION)
    set(RUSTC_VERSION "unknown")
endif()
message(STATUS "Found rustc: ${RUSTC_EXECUTABLE} (version ${RUSTC_VERSION})")
if (DEFINED RUSTC_TARGET)
    message(STATUS "Found Rust target: ${RUSTC_TARGET}")
else()
    message(FATAL_ERROR "Cannot get Rust target")
endif()
find_program(CARGO_EXECUTABLE cargo REQUIRED)
execute_process(
    COMMAND ${CARGO_EXECUTABLE} -V
    OUTPUT_VARIABLE "CARGO_VERSION"
    ERROR_QUIET
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
if (CARGO_VERSION MATCHES "^cargo[ \\t]+")
    string(REGEX REPLACE "^cargo[ \\t]+" "" CARGO_VERSION ${CARGO_VERSION})
else()
    set(CARGO_VERSION "unknown")
endif()
message(STATUS "Found cargo: ${CARGO_EXECUTABLE}, version ${CARGO_VERSION}")

if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/vendor/cxx-rs/gen/cmd/Cargo.toml)
    set(CXXBRIDGE_EXECUTABLE "${CMAKE_CURRENT_BINARY_DIR}/build-tools/bin/cxxbridge")
    add_custom_command(OUTPUT ${CXXBRIDGE_EXECUTABLE}
                       COMMAND ${CARGO_EXECUTABLE} install --root ${CMAKE_CURRENT_BINARY_DIR}/build-tools --path ${CMAKE_CURRENT_SOURCE_DIR}/vendor/cxx-rs/gen/cmd
                       COMMENT "Building Rust command-line tool cxxbridge")
    add_custom_target(cxxbridge
                      DEPENDS ${CXXBRIDGE_EXECUTABLE})
else()
    message(FATAL_ERROR "Cannot find cxx-rs")
endif()

if (CMAKE_BUILD_TYPE STREQUAL "Debug")
    set(CARGO_FLAGS "")
    set(CARGO_OUT "debug")
else()
    set(CARGO_FLAGS "-r")
    set(CARGO_OUT "release")
endif()
